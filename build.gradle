buildscript {
    repositories {
        maven { url 'https://dl.bintray.com/alphagov/maven-test' }
        jcenter()
    }
    dependencies {
        classpath 'com.github.spullara.mustache.java:compiler:0.8.10',
                'org.yaml:snakeyaml:1.10',
                'com.google.guava:guava:14.0.1',
                'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

ext {
    opensaml_version = '3.4.2'
    dropwizard_version = '1.3.9'
    utils_version = '2.0.0-351'
    saml_lib_version = "${opensaml_version}-188"
    build_version = "$opensaml_version-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
}

subprojects {

    apply plugin: 'java'

    group = "uk.gov.ida"
    version = "0.1.$version"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        maven { url 'https://dl.bintray.com/alphagov/maven-test' }
        maven { url 'https://dl.bintray.com/alphagov/maven' }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
        jcenter()
    }

    sourceSets {
        test {
            resources.srcDirs += ["src/dist"]
        }
    }

    configurations {
        common
        dropwizard
        opensaml
        eidas_saml
        ida_utils
        verify_saml
        verify_saml_test
        proxy_node_test
        soft_hsm
    }
//    If any configurations are added/changed please ensure their dependencies are still being tested and monitored by
//    Snyk. See the Snyk directory in the root of the project.
    dependencies {
        configurations.all {
            resolutionStrategy.dependencySubstitution {
                substitute module("dom4j:dom4j:1.6.1") because "dom4j vulnerable to XML External Entity (XXE) Injection due to not validating the QName inputs, see CVE-2018-1000632" with module("org.dom4j:dom4j:2.1.1")
                substitute module("org.eclipse.jetty:jetty-server") because "jetty-server dependency org.eclipse.jetty:jetty-util:9.4.14.v20181114 is vulnerable to CVE-2019-10247" with module("org.eclipse.jetty:jetty-server:9.4.17.v20190418")
                
            }
        }

        common(
                "javax.xml.bind:jaxb-api:2.3.0",
                "javax.activation:activation:1.1.1",
                "io.lettuce:lettuce-core:5.1.4.RELEASE",
        )

        dropwizard(
                "io.dropwizard:dropwizard-core:${dropwizard_version}",
                "io.dropwizard:dropwizard-client:${dropwizard_version}",
                "io.dropwizard:dropwizard-views-mustache:${dropwizard_version}",
                "io.dropwizard:dropwizard-views-mustache:${dropwizard_version}",
                "uk.gov.ida:dropwizard-logstash:1.3.5-68"
        )

        opensaml(
                "org.opensaml:opensaml-core:${opensaml_version}",
                "org.opensaml:opensaml-saml-impl:${opensaml_version}",
                "org.opensaml:opensaml-storage-impl:${opensaml_version}",
        )

        constraints {
            opensaml("commons-collections:commons-collections:3.2.2") {
                because "opensaml:3.4.0 has a dep on commons-collections:3.2.1 which is vulnerable to CVE-2015-7501"
            }
            opensaml("org.bouncycastle:bcprov-jdk15on:1.60") {
                because "opensaml:3.4.0 has a dep on bcprov-jdk15on:1.59 which is vulnerable to CVE-2018-1000180"
            }
            opensaml("com.google.guava:guava:24.1.1-jre") {
                because "opensaml:3.4.0 has a dep on guava:20.0.0 which is vulnerable to CVE-2018-10237"
            }
        }

        eidas_saml(
                "se.litsec.eidas:eidas-opensaml3:1.3.1",
                "se.litsec.opensaml:opensaml3-ext:1.2.2",
        )

        ida_utils(
                "uk.gov.ida:rest-utils:${utils_version}"
        )

        verify_saml(
                "uk.gov.ida:saml-lib:${saml_lib_version}",
                "uk.gov.ida:security-utils:${utils_version}",
        )

        constraints {
            verify_saml("org.bouncycastle:bcprov-jdk15on:1.60") {
                because "saml_lib_version 185 has a dep on bcprov-jdk15on:1.59 which is vulnerable to CVE-2018-1000180"
            }
        }

        verify_saml_test(
                "uk.gov.ida:saml-test:${saml_lib_version}",
        )

        proxy_node_test(
                "net.sourceforge.htmlcleaner:htmlcleaner:2.22",
                "net.sourceforge.htmlunit:htmlunit:2.28",
        )

        testCompile(
                "org.junit.jupiter:junit-jupiter-api:5.4.0",
                'org.junit.jupiter:junit-jupiter-params:5.4.0',
                "org.assertj:assertj-core:3.11.1",
                "org.mockito:mockito-core:2.25.0",
                "org.mockito:mockito-junit-jupiter:2.17.0",
                "io.dropwizard:dropwizard-testing:${dropwizard_version}",
                "net.sourceforge.htmlunit:htmlunit:2.28",
                "com.github.stefanbirkner:system-rules:1.19.0",
                "com.github.fppt:jedis-mock:0.1.9",
                "it.ozimov:embedded-redis:0.7.2",
                "uk.gov.ida:ida-dev-pki:1.1.0-37",
                project(':proxy-node-test'),
        )

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }
}

defaultTasks 'clean', 'test'
