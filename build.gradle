apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'uk.gov.ida.notification.EidasProxyNodeApplication'

buildscript {
    repositories {
        mavenCentral()
    }
}

project.ext {
    dropwizardVersion = '1.2.0'
    openSamlVersion = '3.3.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile ( 
        "io.dropwizard:dropwizard-core:${dropwizardVersion}",
        "io.dropwizard:dropwizard-client:${dropwizardVersion}",
        "org.opensaml:opensaml-core:${openSamlVersion}",
        "org.opensaml:opensaml-saml-impl:${openSamlVersion}",
        "se.litsec.eidas:eidas-opensaml3:1.0.4"
    )
    testCompile(
        'junit:junit:4.12',
        "io.dropwizard:dropwizard-testing:${dropwizardVersion}",
        'com.github.tomakehurst:wiremock-standalone:2.10.1'
    )
}

task pushToPaas(type: Exec) {
    commandLine 'cf', 'push'
}

pushToPaas.dependsOn distZip

task wrapper(type: Wrapper) {
  gradleVersion = '3.3'
}

run {
  args 'server', './src/dist/config.yml'
}

// Acceptance Tests
sourceSets {
    acceptanceTest {
        java.srcDir file('src/acceptance-test/java')
        resources.srcDir file('src/acceptance-test/resources')
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task acceptanceTest(type: Test) {
    description = 'Runs the acceptance tests'
    group = 'verification'
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    reports.junitXml.destination = '$buildDir/acceptance-test-results'
    reports.html.destination = '$buildDir/reports/acceptance-test'
    dependsOn(test)
}
