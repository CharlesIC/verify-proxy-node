dependencies {
    compile configurations.dropwizard,
            configurations.saml,
            configurations.common
}

mainClassName = 'uk.gov.ida.notification.EidasProxyNodeApplication'
version = "1.0.0-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

run {
    args 'server', './src/dist/config.yml'
}

distZip {
    archiveName baseName + '.zip'
}

sourceSets {
    acceptanceTest {
        java.srcDir file('src/acceptance-test/java')
        resources.srcDir file('src/acceptance-test/resources')
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task acceptanceTest(type: Test) {
    description = 'Runs the acceptance tests'
    group = 'verification'
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    setTestClassesDirs sourceSets.acceptanceTest.output.classesDirs
    reports.junitXml.setDestination new File("$buildDir/acceptance-test-results")
    reports.html.setDestination new File("$buildDir/reports/acceptance-test")
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = "uk.gov.ida"
            artifactId = "eidas-notification"

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "/srv/maven"
        }
    }
}
